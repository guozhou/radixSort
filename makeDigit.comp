#version 450
const uint chunkSize = 256;
layout (local_size_x = chunkSize / 4) in; // 1 thread : 4 items (i.e. 1 uvec4)

layout (binding = 0) readonly buffer Key {
	uvec4 key[];
};
layout(binding = 1) writeonly buffer Bin {
    uvec4 bin[];
};

layout(location = 0) uniform int bitOffset;
layout(location = 1) uniform int num;

void main() {
	uint gid = gl_GlobalInvocationID.x * 4;
	if(gid >= num) return; // exit excessive threads in the right-most group

	uvec4 digit = bitfieldExtract(key[gl_GlobalInvocationID.x], bitOffset, 2);

	bin[gid + 0] = uvec4(equal(digit.xxxx, uvec4(0, 1, 2, 3)));
	bin[gid + 1] = uvec4(equal(digit.yyyy, uvec4(0, 1, 2, 3)));
	bin[gid + 2] = uvec4(equal(digit.zzzz, uvec4(0, 1, 2, 3)));
	bin[gid + 3] = uvec4(equal(digit.wwww, uvec4(0, 1, 2, 3)));
}